plugins {
    id 'fabric-loom' version '1.11.4'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'checkstyle'
    id 'pmd'
    id 'com.modrinth.minotaur' version '2.8.8'
}

ext {
    checkstyleVersion = '10.26.1'

    pmdVersion = '7.16.0'

    // Derive version from yarn
    minecraft_version = project.yarn_mappings.split("\\+")[0]
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

loom {
    accessWidenerPath = file("src/main/resources/cape-provider.accesswidener")
    log4jConfigs.from(file("src/dev/log4j2.xml"))
}

// https://github.com/gradle/gradle/issues/27035
configurations.checkstyle {
    resolutionStrategy.capabilitiesResolution.withCapability("com.google.collections:google-collections") {
        select("com.google.guava:guava:0")
    }
}

repositories {
    exclusiveContent {
        forRepository {
            maven {
                name = "Terraformers"
                url = "https://maven.terraformersmc.com/"
            }
        }
        filter {
            includeGroup 'com.terraformersmc'
        }
    }
    exclusiveContent {
        forRepository {
            maven {
                name = "DevAuth"
                url = "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1"
            }
        }
        filter {
            includeGroup 'me.djtheredstoner'
        }
    }
    // https://support.modrinth.com/en/articles/8801191-modrinth-maven
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup 'maven.modrinth'
        }
    }
    // Fallback (fixes Renovate)
    mavenCentral()
}

dependencies {
    checkstyle "com.puppycrawl.tools:checkstyle:${checkstyleVersion}"

    pmd "net.sourceforge.pmd:pmd-ant:${pmdVersion}",
            "net.sourceforge.pmd:pmd-java:${pmdVersion}"

    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    // Required when translations and icons should be loaded correctly
    modImplementation "net.fabricmc.fabric-api:fabric-api:0.130.0+1.21.8"

    modCompileOnly "com.terraformersmc:modmenu:${project.modmenu_version}"
    modCompileOnly "maven.modrinth:skinshuffle:${project.skinshuffle_version}"
    modCompileOnly "maven.modrinth:cicada:${project.cicada_version}"
    modCompileOnly "maven.modrinth:minecraftcapes:${project.minecraftcapes_version}"

    // Only needed during development
    modLocalRuntime "com.terraformersmc:modmenu:${project.modmenu_version}"
    modLocalRuntime "net.fabricmc.fabric-api:fabric-api:0.130.0+1.21.8"
    modLocalRuntime "me.djtheredstoner:DevAuth-fabric:1.2.1"
    modLocalRuntime "maven.modrinth:no-chat-reports:Fabric-1.21.7-v2.14.0"
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "id": project.archives_base_name,
                "name": project.mod_name,
                "desc": project.mod_desc,
                "license_spdx_id": project.mod_license_spdx_id,
                "minecraft_version": minecraft_version,
                "loader_version": project.loader_version,
                "modmenu_version": project.modmenu_version
    }
}

checkstyle {
    configDirectory = file("$rootProject.projectDir/.config/checkstyle")
    ignoreFailures = false
    showViolations = true
    toolVersion = checkstyleVersion
}

pmd {
    consoleOutput = true
    ruleSetFiles = files(".config/pmd/java/ruleset.xml")
    toolVersion = pmdVersion
}

tasks.withType(Checkstyle).configureEach {
    enabled = project.hasProperty("checkstyleEnabled");
}

tasks.withType(Pmd).configureEach {
    enabled = project.hasProperty("pmdEnabled")
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    // Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
    // if it is present.
    // If you remove this line, sources will not be generated.
    withSourcesJar()
    withJavadocJar()

    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

javadoc {
    options.addBooleanOption('html5', true)
    options.addBooleanOption('Xdoclint:none', true)
    options.addBooleanOption('quiet', true)
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

// configure the maven publication
publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
            pom {
                name = project.mod_name
                description = project.mod_desc
                url = 'https://github.com/litetex-oss/mcm-' + artifactId
                licenses {
                    license {
                        name = project.mod_license_spdx_id
                        url = project.mod_license_url
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        name = 'litetex'
                        url = 'https://github.com/litetex'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/litetex-oss/' + artifactId + '.git'
                    url = 'https://github.com/litetex-oss/mcm-' + artifactId
                }
            }
        }
    }
}

tasks.modrinth.dependsOn(tasks.modrinthSyncBody)
// https://github.com/modrinth/minotaur/blob/master/README.md#available-properties
modrinth {
    // token defaults to "MODRINTH_TOKEN" environment variable
    projectId = project.archives_base_name
    // Default/empty value is release
    versionType = System.getenv("MODRINTH_VERSION_TYPE")
    uploadFile = remapJar
    additionalFiles = [sourcesJar]
    syncBodyFrom = rootProject.file("README.md").text
    changelog = System.getenv("MODRINTH_CHANGELOG_TEXT")
    dependencies {
        required.project 'fabric-api'
        optional.project 'modmenu'
        incompatible.project 'capes'
    }
}

signing {
    def signingKey = System.getenv("MAVEN_GPG_PRIVATE_KEY")
    def signingPassword = System.getenv("MAVEN_GPG_PASSPHRASE")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

// Docs: https://github.com/gradle-nexus/publish-plugin
// Publish using publishToSonatype closeAndReleaseSonatypeStagingRepository
nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
            username = System.getenv("SONATYPE_MAVEN_CENTRAL_PORTAL_USERNAME")
            password = System.getenv("SONATYPE_MAVEN_CENTRAL_PORTAL_TOKEN")
        }
    }
}
